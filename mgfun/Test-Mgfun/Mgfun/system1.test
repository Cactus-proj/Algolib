# Copyright (C) 1991--2010 by INRIA.
#
# This file is part of Algolib.
#
# Algolib is free software: you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# Algolib is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with Algolib.  If not, see
# <http://www.gnu.org/licenses/>.

with(Ore_algebra):
with(Mgfun):

cmp:=proc(a,b)
    local la,lb;
    la:=expand(a);
    lb:=expand(b);
    if remove(member,remove(member,la,lb),map(p->-p,lb))={}
    and nops(la)=nops(lb) then
	okay
    else
	la
    fi
end:

# The following function should be made available to the user
# as a package member.
rec:=Mgfun:-MG_Internals:-recognize_operator_algebra:

# binomial(n,k)
sys:={
    (n+1-k)*h(n+1,k)-(n+1)*h(n,k),
    (k+1)*h(n,k+1)-(n-k)*h(n,k)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
traperror(sum_of_sys(sys,k=-infinity..infinity)):
if [%]=["non homogeneous case not implemented yet"]
then okay else % fi;
sum_of_sys(sys,k=-infinity..infinity,_natural_boundaries):
if %={h(n+1)-2*h(n)} then okay else % fi;

# binomial(n,k)^2
sys:={
    (n+1-k)^2*h(n+1,k)-(n+1)^2*h(n,k),
    (k+1)^2*h(n,k+1)-(n-k)^2*h(n,k)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
sum_of_sys(sys,k=-infinity..infinity,_natural_boundaries):
if %={(-n-1)*h(n+1)+(4*n+2)*h(n)} or %={(-4*n-2)*h(n)+(n+1)*h(n+1)}
then okay else % fi;

# Apery numbers
sys:={
    (-n-1+k)^2*h(n+1,k)-(n+1+k)^2*h(n,k),
    -(n+1+k)^2*(-n+k)^2*h(n,k)+(k+1)^4*h(n,k+1)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
sum_of_sys(sys,k=-infinity..infinity,_natural_boundaries):
collect(%,h,factor):
if %={-(n+2)^3*h(n+2)+(2*n+3)*(17*n^2+51*n+39)*h(n+1)-(n+1)^3*h(n)}
or %={(n+1)^3*h(n)+(2+n)^3*h(2+n)-(17*n^2+51*n+39)*(2*n+3)*h(n+1)}
then okay else % fi;

# orthopoly[P](n,a,b,x)
sys:={
    2*(n+a+1)*(n+b+1)*(2*n+a+b+4)*h(n,x)+(-(2*n+a+b+3)*(a^2-b^2)-(2*n+a+b+2)*(2*n+a+b+3)*(2*n+a+b+4)*x)*h(n+1,x)+2*(n+2)*(n+a+b+2)*(2*n+a+b+2)*h(n+2,x),
    -2*(n+a+1)*(n+b+1)*h(n,x)-(n+1)*(a-b-(2*n+a+b+2)*x)*h(n+1,x)+(2*n+a+b+2)*(1-x^2)*D[2](h)(n+1,x),
    n*(n+a+b+1)*h(n,x)+(1-x^2)*diff(diff(h(n,x),x),x)+(b-a-(a+b+2)*x)*diff(h(n,x),x)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);

# (-1)^k*q^((5*k^2-k)/2)/qfactorial(q,n-k)/qfactorial(q,n+k)
sys:={
    (-q^(5*k+2)+q^(4*k+2+n))*h(n,k)+(-1+q^(n+1+k))*h(n,k+1),
    -q^k*h(n,k)+(q^k-q^(n+1+2*k)-q^(n+1)+q^(2+2*n+k))*h(n+1,k)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
if false then
sum_of_sys(sys,k=-infinity..infinity,_takayama_algo):
collect(%,h,factor):
if %=
{-q^10*h(n)+q*(q^5+2*q^4+q+q^16*(q^n)^4+2*q^2+q^10*(q^n)^2+q^9*(q^n)^2+q^8*(q^
n)^2+2*q^3+q^17*(q^n)^4+q^24*(q^n)^6+q^6+1)*h(n+3)-q^3*(2*q^9*(q^n)^2+q^5+2*q^
3+q^16*(q^n)^4+q+q^6+q^11*(q^n)^2+q^15*(q^n)^4+2*q^2+2*q^8*(q^n)^2+q^7*(q^n)^2
+2*q^4+q^14*(q^n)^4+q^10*(q^n)^2+1)*h(n+2)+q^6*(q^6*(q^n)^2+q^7*(q^n)^2+1+q^4+
q+q^2+q^8*(q^n)^2+q^3)*h(n+1)+(-q^2-q^4+q^10*(q^n)^2+q^12*(q^n)^2+q^22*(q^n)^5
+q^23*(q^n)^5-q^3+q^11*(q^n)^2-1-q)*h(n+4)+(q^5*q^n-1)*(q^5*q^n+1)*(q^9*(q^n)^
2-1)*h(n+5)}
then okay else % fi;
sum_of_sys(sys,n=-infinity..infinity,_natural_boundaries):
collect(%,h,factor):
if %=
{q^4*(q^k)^6*((q^k)^2*q^3-1)*h(k)+(q*(q^k)^2-1)*h(k+2)+q^2*q^k*(q^k*q-1)*(q^k*
q+1)*((q^k)^4*q^4-(q^k)^2*q^3+(q^k)^2*q^2+1)*h(k+1)}
then okay else % fi;
end if :

# qpochhammer(q,n,x*y^2)
sys:={
    (-1+x*y^2)*h(q*x,y,n)+(1-q^n*x*y^2)*h(x,y,n),
    (-1+x*y^2)*(-1+q*x*y^2)*h(x,q*y,n)
	-(-1+q^n*x*y^2)*(-1+x*y^2*q*q^n)*h(x,y,n),
    h(x,y,n+1)+(-1+x*y^2*q^n)*h(x,y,n)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
sum_of_sys(sys,n=-infinity..infinity,_natural_boundaries):
if %=
{h(x,y)+(-1+x*y^2)*h(q*x,y), h(x,y)+(-1+q*x*y^2+x*y^2-x^2*y^4*q)*h(x,q*y)}
or %=
{h(x,y)+(-1+x*y^2)*h(q*x,y), -h(x,y)+(q*y^4*x^2+1-q*x*y^2-x*y^2)*h(x,q*y)}
or %=
{h(x,y)+(-1+x*y^2)*h(q*x,y), -h(x,y)+(q*y^4*x^2+1+(-q-1)*x*y^2)*h(x,q*y)}
then okay else % fi;

# qbinomial(q,n,k)
sys:={
    -q^k*(-1+q*q^n)*h(n,k)+(-q^k+q*q^n)*h(n+1,k),
    (q^k-q^n)*h(n,k)+q^k*(-1+q*q^k)*h(n,k+1)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
sum_of_sys(sys,k=-infinity..infinity,_natural_boundaries):
if %={(1-q*q^n)*h(n)-2*h(n+1)+h(n+2)} then okay else % fi;

# Error 1
sys:={
    diff(f(x+1),x)
}:
us:=traperror(rec(sys)):
if [us]=["too complicated functional equations",4,5,{x},{x}]
then okay else us fi;

# Error 2
sys:={
    f(x+1,y),
    g(x,q*y)
}:
us:=traperror(rec(sys)):
if [us]=["multiple functions not dealt with yet"]
then okay else us fi;

# q-dilation
sys:={
    f(q^3*x,q^2*y)-f(x,y)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);

# Bessel functions
sys:={
    x^2*diff(f(x),x,x)+x*diff(f(x),x)+(x^2-nu^2)*f(x)
}:
us:=rec(sys):
map(applyopr,us["system"],us["function"],us["algebra"]):
cmp(%,sys);
