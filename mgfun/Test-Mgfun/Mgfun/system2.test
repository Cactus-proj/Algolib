# Copyright (C) 1991--2013 by INRIA.
#
# This file is part of Algolib.
#
# Algolib is free software: you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# Algolib is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with Algolib.  If not, see
# <http://www.gnu.org/licenses/>.

with(Mgfun):
with(Ore_algebra):
with(Holonomy):

# Sum(binomial(n,k),k=0..n).
sys1:={(n+1-k)*h(n+1,k)-(n+1)*h(n,k),(k+1)*h(n,k+1)-(n-k)*h(n,k)}:
traperror(sum_of_sys(sys1,k=-infinity..infinity,_incautious)):
if [%]=["non homogeneous case not implemented yet"]
then okay else % fi;
sum_of_sys(sys1,k=-infinity..infinity,_natural_boundaries):
if %={h(n+1)-2*h(n)} then okay else % fi;
# This is 2^n.

# Sum(binomial(n,k)^2,k=0..n).
sys2:={(n+1-k)^2*h(n+1,k)-(n+1)^2*h(n,k),(k+1)^2*h(n,k+1)-(n-k)^2*h(n,k)}:
sum_of_sys(sys2,k=-infinity..infinity,_natural_boundaries):
if %={(-n-1)*h(n+1)+(4*n+2)*h(n)} or %={(n+1)*h(n+1)+(-4*n-2)*h(n)}
then okay else % fi;
# This is binomial(2*n,n).

# Diagonal of 1-(1-x-y).
A:=diff_algebra([Ds,s],[Dx,x]):
f:=1/(1-x-y):
normal(subs({x=s,y=x/s},f)/s):
G:=hypergeom_to_dfinite(%,A):
sys3:=map(applyopr,G,h(s,x),A):
int_of_sys(sys3,s=residues):
if %={(1-4*x)*diff(h(x),x)-2*h(x)} or %={(4*x-1)*diff(h(x),x)+2*h(x)}
then okay else % fi;
int_of_sys(sys3 union {diff(h(s,x),s)+(1-2*s)*diff(h(s,x),x)},s=residues):
if %={(1-4*x)*diff(h(x),x)-2*h(x)} or %={2*h(x)+(4*x-1)*diff(h(x),x)}
then okay else % fi;
# This is 1/sqrt(1-4*x).

# Diagonal of 1-sqrt(1-4*(x+y)).
f:=1/sqrt(1-4*(x+y)):
normal(subs({x=s,y=x/s},f)/s):
G:=hypergeom_to_dfinite(%,A):
sys4:=map(applyopr,G,h(s,x),A):
int_of_sys(sys4,s=residues):
if %={12*h(x)+(-x+64*x^2)*diff(diff(h(x),x),x)+(128*x-1)*diff(h(x),x)}
then okay else % fi;
# This is hypergeom([1/4,3/4],[1],64*x).

# Diagonal of 1/(1-(x^2+x*y+y^2)).
f:=1/(1-(x^2+x*y+y^2)):
normal(subs({x=s,y=x/s},f)/s):
G:=hypergeom_to_dfinite(%,A):
sys5:=map(applyopr,G,h(s,x),A):
int_of_sys(sys5,s=residues):
if %={(1+3*x)*h(x)+(2*x+3*x^2-1)*diff(h(x),x)} then okay else % fi;
# This is 1/(1+x)^(1/2)/(1-3*x)^(1/2).

# Diagonal of 1/(1-z)/(1-y-x*z).
A:=diff_algebra(comm=x,[Ds,s],[Dy,y]):
f:=1/(1-z)/(1-y-x*z):
normal(subs({y=s,z=y/s},f)/s):
G:=hypergeom_to_dfinite(%,A):
sys6:=map(applyopr,G,h(s,y),A):
int_of_sys(sys6,s=residues):
if %={6*x*h(y)+(6*x^2+14*x*y-6*x-2)*diff(h(y),y)
  +(4*x^2*y+4*x*y^2-4*x*y-x-y+1)*diff(diff(h(y),y),y)}
or %={-6*x*h(y)+(-6*x^2-14*x*y+6*x+2)*diff(h(y),y)
  +(-4*x^2*y-4*x*y^2+4*x*y+x+y-1)*diff(diff(h(y),y),y)}
then okay else % fi;
# This is (1+(1-2*x)/sqrt(1-4*x*y))/(1-x-y)/2.
